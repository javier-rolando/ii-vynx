import qs.services
import qs.modules.common
import qs.modules.common.functions
import Qt5Compat.GraphicalEffects
import QtQuick
import QtQuick.Layouts
import Quickshell
import Quickshell.Widgets
import Quickshell.Hyprland

DockButton {
    id: root
    property var appToplevel
    property var appListRoot
    property int lastFocused: -1
    property real iconSize: 35
    property real countDotWidth: 10
    property real countDotHeight: 4
    property bool appIsActive: appToplevel.toplevels.find(t => (t.activated == true)) !== undefined

    readonly property bool isSeparator: appToplevel.appId === "SEPARATOR"
    readonly property var desktopEntry: DesktopEntries.heuristicLookup(appToplevel.appId)
    enabled: !isSeparator
    implicitWidth: isSeparator ? 1 : implicitHeight - topInset - bottomInset

    Loader {
        active: isSeparator
        anchors {
            fill: parent
            topMargin: dockVisualBackground.margin + dockRow.padding + Appearance.rounding.normal
            bottomMargin: dockVisualBackground.margin + dockRow.padding + Appearance.rounding.normal
        }
        sourceComponent: DockSeparator {}
    }

    Loader {
        anchors.fill: parent
        active: appToplevel.toplevels.length > 0
        sourceComponent: MouseArea {
            id: mouseArea
            anchors.fill: parent
            hoverEnabled: true
            acceptedButtons: Qt.NoButton
            onEntered: {
                appListRoot.lastHoveredButton = root
                appListRoot.buttonHovered = true
                lastFocused = appToplevel.toplevels.length - 1
            }
            onExited: {
                if (appListRoot.lastHoveredButton === root) {
                    appListRoot.buttonHovered = false
                }
            }
        }
    }

    onClicked: {
        const specialWorkspaces = {
            "kitty-special": "term",
            "kitty-yazi": "yazi",
            "kitty-update": "update",
            "kitty-install": "install",
            "kitty-uninstall": "uninstall",
            "kitty-btop": "btop",
            "kitty-english": "english",
            // "eu.betterbird.betterbird": "betterbird",
            // "ferdium": "ferdium",
            // "vesktop": "vesktop",
            "spotify": "spotify",
            "chrome-translate.google.com__-default": "translate"
        };

        if (appToplevel.appId === "chrome-chat.openai.com__-default") {
            if (appToplevel.toplevels.length === 0) {
                Hyprland.dispatch('exec chromium --app="https://chat.openai.com"');
            } else {
                lastFocused = (lastFocused + 1) % appToplevel.toplevels.length;
                appToplevel.toplevels[lastFocused].activate();
            }
            return;
        }

        if (appToplevel.appId === "chrome-gemini.google.com__app-default") {
            if (appToplevel.toplevels.length === 0) {
                Hyprland.dispatch('exec chromium --app="https://gemini.google.com/app"');
            } else {
                lastFocused = (lastFocused + 1) % appToplevel.toplevels.length;
                appToplevel.toplevels[lastFocused].activate();
            }
            return;
        }

        const workspaceName = specialWorkspaces[appToplevel.appId];

        if (workspaceName) {
            Hyprland.dispatch(`togglespecialworkspace ${workspaceName}`);
            return;
        }

        if (appToplevel.toplevels.length === 0) {
            root.desktopEntry?.execute();
            return;
        }
        lastFocused = (lastFocused + 1) % appToplevel.toplevels.length
        appToplevel.toplevels[lastFocused].activate()
    }

    middleClickAction: () => {
        if (appToplevel.appId === "chrome-chat.openai.com__-default") {
            Hyprland.dispatch('exec chromium --app="https://chat.openai.com"');
            return;
        }
        root.desktopEntry?.execute();
    }

    // altAction: () => {
    //     TaskbarApps.togglePin(appToplevel.appId);
    // }

    contentItem: Loader {
        active: !isSeparator
        sourceComponent: Item {
            anchors.centerIn: parent

            Loader {
                id: iconImageLoader
                anchors {
                    left: parent.left
                    right: parent.right
                    verticalCenter: parent.verticalCenter
                }
                active: !root.isSeparator
                sourceComponent: IconImage {
                    source: Quickshell.iconPath(AppSearch.guessIcon(appToplevel.appId), "image-missing")
                    implicitSize: root.iconSize
                }
            }

            Loader {
                active: Config.options.dock.monochromeIcons
                anchors.fill: iconImageLoader
                sourceComponent: Item {
                    Desaturate {
                        id: desaturatedIcon
                        visible: false // There's already color overlay
                        anchors.fill: parent
                        source: iconImageLoader
                        desaturation: 0.8
                    }
                    ColorOverlay {
                        anchors.fill: desaturatedIcon
                        source: desaturatedIcon
                        color: ColorUtils.transparentize(Appearance.colors.colPrimary, 0.9)
                    }
                }
            }

            RowLayout {
                spacing: 3
                anchors {
                    top: iconImageLoader.bottom
                    topMargin: 2
                    horizontalCenter: parent.horizontalCenter
                }
                Repeater {
                    model: Math.min(appToplevel.toplevels.length, 3)
                    delegate: Rectangle {
                        required property int index
                        radius: Appearance.rounding.full
                        implicitWidth: (appToplevel.toplevels.length <= 3) ? 
                            root.countDotWidth : root.countDotHeight // Circles when too many
                        implicitHeight: root.countDotHeight
                        color: appIsActive ? Appearance.colors.colPrimary : ColorUtils.transparentize(Appearance.colors.colOnLayer0, 0.4)
                    }
                }
            }
        }
    }
}
