import qs
import qs.services
import qs.modules.common
import qs.modules.common.widgets
import QtQuick
import QtQuick.Controls
import QtQuick.Effects
import QtQuick.Layouts
import Quickshell.Io
import Quickshell
import Quickshell.Widgets
import Quickshell.Wayland
import Quickshell.Hyprland
import Quickshell.Services.Mpris
import Qt5Compat.GraphicalEffects

Scope { // Scope
    id: root
    property bool pinned: Config.options?.dock.pinnedOnStartup ?? false

    Variants {
        // For each monitor
        model: Quickshell.screens

        PanelWindow {
            id: dockRoot
            // Window
            required property var modelData
            screen: modelData
            visible: !GlobalStates.screenLocked

            property bool reveal: root.pinned || (Config.options?.dock.hoverToReveal && dockMouseArea.containsMouse) || dockApps.requestDockShow || (!ToplevelManager.activeToplevel?.activated)

            property bool isBarBottom: !Config.options.bar.vertical && Config.options.bar.bottom

            anchors {
                bottom: !isBarBottom
                top: isBarBottom
                left: true
                right: true
            }

            exclusiveZone: root.pinned ? implicitHeight - (Appearance.sizes.hyprlandGapsOut) - (Appearance.sizes.elevationMargin - Appearance.sizes.hyprlandGapsOut) : 0

            implicitWidth: dockBackground.implicitWidth
            WlrLayershell.namespace: "quickshell:dock"
            WlrLayershell.layer: WlrLayer.Overlay
            color: "transparent"

            implicitHeight: (Config.options?.dock.height ?? 70) + Appearance.sizes.elevationMargin + Appearance.sizes.hyprlandGapsOut

            mask: Region {
                item: dockMouseArea
            }

            MouseArea {
                id: dockMouseArea
                height: parent.height
                anchors {
                    topMargin: isBarBottom ? 0 : dockRoot.reveal ? 0 : Config.options?.dock.hoverToReveal ? (dockRoot.implicitHeight - Config.options.dock.hoverRegionHeight) : (dockRoot.implicitHeight + 1)
                    bottomMargin: isBarBottom ? dockRoot.reveal ? 0 : Config.options?.dock.hoverToReveal ? (dockRoot.implicitHeight - Config.options.dock.hoverRegionHeight) : (dockRoot.implicitHeight + 1) : 0
                    horizontalCenter: parent.horizontalCenter
                }
                implicitWidth: dockHoverRegion.implicitWidth + Appearance.sizes.elevationMargin * 2
                hoverEnabled: true

                state: isBarBottom ? "bottomAnchored" : "topAnchored"
                states: [
                    State {
                        name: "topAnchored"
                        AnchorChanges {
                            target: dockMouseArea
                            anchors.top: parent.top
                            anchors.bottom: undefined
                        }
                    },
                    State {
                        name: "bottomAnchored"
                        AnchorChanges {
                            target: dockMouseArea
                            anchors.top: undefined
                            anchors.bottom: parent.bottom
                        }
                    }
                ]

                Behavior on anchors.topMargin {
                    animation: Appearance.animation.elementMoveFast.numberAnimation.createObject(this)
                }

                Behavior on anchors.bottomMargin {
                    animation: Appearance.animation.elementMoveFast.numberAnimation.createObject(this)
                }

                Item {
                    id: dockHoverRegion
                    anchors.fill: parent
                    implicitWidth: dockBackground.implicitWidth

                    Item { // Wrapper for the dock background
                        id: dockBackground
                        anchors {
                            top: parent.top
                            bottom: parent.bottom
                            horizontalCenter: parent.horizontalCenter
                        }

                        implicitWidth: dockRow.implicitWidth + 5 * 2
                        height: parent.height - Appearance.sizes.elevationMargin - Appearance.sizes.hyprlandGapsOut

                        StyledRectangularShadow {
                            target: dockVisualBackground
                        }
                        Rectangle { // The real rectangle that is visible
                            id: dockVisualBackground
                            property real margin: Appearance.sizes.elevationMargin
                            anchors.fill: parent
                            anchors.topMargin: Appearance.sizes.elevationMargin
                            anchors.bottomMargin: Appearance.sizes.hyprlandGapsOut
                            color: Appearance.colors.colLayer0
                            border.width: 1
                            border.color: Appearance.colors.colLayer0Border
                            radius: Appearance.rounding.large
                        }

                        RowLayout {
                            id: dockRow
                            anchors.top: parent.top
                            anchors.bottom: parent.bottom
                            anchors.horizontalCenter: parent.horizontalCenter
                            spacing: 3
                            property real padding: 5

                            VerticalButtonGroup {
                                Layout.topMargin: Appearance.sizes.hyprlandGapsOut // why does this work
                                GroupButton {
                                    // Pin button
                                    baseWidth: 35
                                    baseHeight: 35
                                    clickedWidth: baseWidth
                                    clickedHeight: baseHeight + 20
                                    buttonRadius: Appearance.rounding.normal
                                    toggled: root.pinned
                                    onClicked: root.pinned = !root.pinned
                                    contentItem: MaterialSymbol {
                                        text: "keep"
                                        horizontalAlignment: Text.AlignHCenter
                                        iconSize: Appearance.font.pixelSize.larger
                                        color: root.pinned ? Appearance.m3colors.m3onPrimary : Appearance.colors.colOnLayer0
                                    }
                                }
                            }
                            DockSeparator {}
                            DockApps {
                                id: dockApps
                                buttonPadding: dockRow.padding
                            }
                            DockSeparator {}

                            // Media Player 
                            Item {
                                id: mediaPlayerItem
                                Layout.fillHeight: true
                                Layout.topMargin: 16
                                Layout.leftMargin: 5
                                Layout.bottomMargin: Appearance.sizes.hyprlandGapsOut + dockRow.padding
                                
                                function getSpotifyPlayer() {
                                  var sourceList = MprisController.players;

                                  if (!sourceList || sourceList.length === 0) {
                                    sourceList = MprisController.allPlayers || [];
                                  }

                                  for (var i = 0; i < sourceList.length; i++) {
                                    var player = sourceList[i];
                                    if (player && (player.desktopEntry === "spotify" || player.dbusName.indexOf("spotify") !== -1)) {
                                      return player;
                                    }
                                  }
                                  return null;
                                }

                                Connections {
                                    target: MprisController
                                    function onPlayersChanged() {
                                        mediaPlayerItem.activePlayer = getSpotifyPlayer()
                                    }
                                }
                                
                                property var activePlayer: getSpotifyPlayer()
                                property string cleanedTitle: activePlayer?.trackTitle ?? ""
                                visible: activePlayer !== null && cleanedTitle !== ""
                                
                                implicitWidth: visible ? mediaRow.implicitWidth + 16 : 0
                                implicitHeight: parent.height
                                
                                Timer {
                                    running: mediaPlayerItem.activePlayer?.playbackState == MprisPlaybackState.Playing
                                    interval: Config.options.resources.updateInterval
                                    repeat: true
                                    onTriggered: mediaPlayerItem.activePlayer.positionChanged()
                                }
                                
                                Rectangle {
                                    anchors.fill: parent
                                    color: "transparent"
                                    radius: Appearance.rounding.normal
                                    
                                    MouseArea {
                                        anchors.fill: parent
                                        acceptedButtons: Qt.MiddleButton | Qt.BackButton | Qt.ForwardButton | Qt.RightButton | Qt.LeftButton
                                        onPressed: (event) => {
                                            if (event.button === Qt.LeftButton) {
                                                mediaPlayerItem.activePlayer.togglePlaying();
                                            } else if (event.button === Qt.RightButton) {
                                                var spotifyWindow = HyprlandData.windowList.find(function(win) { return win.class && win.class.toLowerCase() === "spotify" });
                                                if (spotifyWindow) {
                                                    Hyprland.dispatch("closewindow address:" + spotifyWindow.address);
                                                }
                                            } else if (event.button === Qt.BackButton) {
                                                mediaPlayerItem.activePlayer.previous();
                                            } else if (event.button === Qt.ForwardButton) {
                                                mediaPlayerItem.activePlayer.next();
                                            }
                                        }
                                        onWheel: (wheel) => {
                                            if (wheel.angleDelta.y > 0) { // Scrolling up
                                                mediaPlayerItem.activePlayer.next();
                                            } else if (wheel.angleDelta.y < 0) { // Scrolling down
                                                mediaPlayerItem.activePlayer.previous();
                                            }
                                        }
                                    }
                                    
                                    RowLayout {
                                        id: mediaRow
                                        anchors.centerIn: parent
                                        spacing: 8
                                        
                                        Image {
                                            Layout.alignment: Qt.AlignVCenter
                                            source: mediaPlayerItem.activePlayer?.trackArtUrl && mediaPlayerItem.activePlayer.trackArtUrl !== "" ? mediaPlayerItem.activePlayer.trackArtUrl : "../../assets/icons/cover.png"
                                            fillMode: Image.PreserveAspectCrop
                                            cache: false
                                            width: 35
                                            height: 35
                                            sourceSize.width: 35
                                            sourceSize.height: 35
                                            
                                            layer.enabled: true
                                            layer.effect: OpacityMask {
                                                maskSource: Rectangle {
                                                    width: 35
                                                    height: 35
                                                    radius: 6
                                                }
                                            }
                                        }
                                        
                                        Column {
                                            Layout.alignment: Qt.AlignVCenter
                                            Layout.preferredWidth: 150
                                            spacing: -2
                                            
                                            StyledText {
                                                width: parent.width
                                                horizontalAlignment: Text.AlignLeft
                                                elide: Text.ElideRight
                                                color: Appearance.colors.colSubtext
                                                text: mediaPlayerItem.activePlayer?.trackArtist ?? "Unknown Artist"
                                                font.pixelSize: 11
                                            }
                                            
                                            StyledText {
                                                width: parent.width
                                                horizontalAlignment: Text.AlignLeft
                                                elide: Text.ElideRight
                                                color: Appearance.colors.colOnLayer1
                                                text: mediaPlayerItem.cleanedTitle !== "" ? mediaPlayerItem.cleanedTitle : "No Title"
                                                font.weight: Font.Medium
                                                font.pixelSize: 13
                                            }
                                        }

                                        MaterialSymbol {
                                            Layout.alignment: Qt.AlignVCenter
                                            text: mediaPlayerItem.activePlayer?.isPlaying ? "pause" : "play_arrow"
                                            iconSize: 23
                                            color: Appearance.colors.colOnLayer1
                                        }
                                    }
                                }
                            }

                            DockSeparator {
                                visible: mediaPlayerItem.visible
                            }

                            DockButton {
                                Layout.fillHeight: true
                                onClicked: GlobalStates.overviewOpen = !GlobalStates.overviewOpen
                                topInset: Appearance.sizes.hyprlandGapsOut + dockRow.padding
                                bottomInset: Appearance.sizes.hyprlandGapsOut + dockRow.padding
                                contentItem: MaterialSymbol {
                                    anchors.fill: parent
                                    horizontalAlignment: Text.AlignHCenter
                                    font.pixelSize: parent.width / 2
                                    text: "apps"
                                    color: Appearance.colors.colOnLayer0
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}
